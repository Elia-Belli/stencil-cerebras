// Color map/ WSE2 task ID map
// On WSE-2, data tasks are bound to colors (IDs 0 through 24)
//
//  ID var                ID var     ID var                ID var
//   0 send_east_color_1   9         18                    27 reserved (memcpy)
//   1 send_east_color_2  10         19                    28 reserved (memcpy)
//   2 send_west_color_1  11         20                    29 reserved
//   3 send_west_color_2  12         21 reserved (memcpy)  30 reserved (memcpy)
//   4 send_north_color_1 13         22 reserved (memcpy)  31 reserved
//   5 send_north_color_2 14         23 reserved (memcpy)  32
//   6 send_south_color_1 15         24                    33
//   7 send_south_color_2 16         25                    34
//   8                    17         26                    35

// WSE-3 task ID map
// On WSE-3, data tasks are bound to input queues (IDs 0 through 7)
//
//  ID var                  ID var  ID var                ID var
//   0 reserved (memcpy)     9      18                    27 reserved (memcpy)
//   1 reserved (memcpy)    10      19                    28 reserved (memcpy)
//   2                      11      20                    29 reserved
//   3                      12      21 reserved (memcpy)  30 reserved (memcpy)
//   4                      13      22 reserved (memcpy)  31 reserved
//   5                      14      23 reserved (memcpy)  32
//   6                      15      24                    33
//   7                      16      25                    34
//   8                      17      26                    35

param kernel_dim_x: i32;
param kernel_dim_y: i32;
param iterations: i64;

// total matrix dimensions
param M: i32;
param N: i32;

// Colors
const send_east_color_1: color = @get_color(0);
const send_east_color_2: color = @get_color(1);
const send_west_color_1: color = @get_color(2);
const send_west_color_2: color = @get_color(3);

const send_north_color_1: color = @get_color(4);
const send_north_color_2: color = @get_color(5);
const send_south_color_1: color = @get_color(6);
const send_south_color_2: color = @get_color(7);


const memcpy = @import_module("<memcpy/get_params>", .{
  .width = kernel_dim_x,
  .height = kernel_dim_y
});

layout {
  // PE coordinates are (column, row)
  @set_rectangle(kernel_dim_x, kernel_dim_y);

  // Comptime assertions
  @comptime_assert(kernel_dim_x > 1, "kernel_dim_x must be greater than 1");
  @comptime_assert(kernel_dim_y > 1, "kernel_dim_y must be greater than 1");
  @comptime_assert(M > 0, "M must be greater than 0");
  @comptime_assert(N > 0, "N must be greater than 0");
  @comptime_assert(iterations > 0, "iterations must be greater than 0");

  // pe parameters
  const pad_x = if(N % kernel_dim_x == 0) 0 else kernel_dim_x - (N % kernel_dim_x);
  const pad_y = if(M % kernel_dim_y == 0) 0 else kernel_dim_y - (M % kernel_dim_y);

  const pe_M = (M + pad_y) / kernel_dim_y; 
  const pe_N = (N + pad_x) / kernel_dim_x;

  @comptime_assert(pe_M * pe_N <= 5300, "The number of elements per cores can't exceed 5300");

  const common_params = .{
    .width = kernel_dim_x,
    .height = kernel_dim_y,
    .iterations = iterations
  };

  const even_col_params = .{
    .send_east_color = send_east_color_1, .recv_west_color = send_east_color_2,
    .send_west_color = send_west_color_1, .recv_east_color = send_west_color_2
  };
  const odd_col_params = .{
    .send_east_color = send_east_color_2, .recv_west_color = send_east_color_1,
    .send_west_color = send_west_color_2, .recv_east_color = send_west_color_1
  };
  const even_row_params = .{
    .send_north_color = send_north_color_1, .recv_south_color = send_north_color_2,
    .send_south_color = send_south_color_1, .recv_north_color = send_south_color_2
  };
  const odd_row_params = .{
    .send_north_color = send_north_color_2, .recv_south_color = send_north_color_1,
    .send_south_color = send_south_color_2, .recv_north_color = send_south_color_1
  }; 

  var pe_pad_x: i32 = 0;
  var pe_pad_y: i32 = 0;

  // Tile Code
  for (@range(i32, kernel_dim_x)) |idx| {
    for (@range(i32, kernel_dim_y)) |idy| {

      var pe_n = ((idx+1) * pe_N -1); // last column index of the current PE
      var pe_m = ((idy+1) * pe_M -1); // last row index of the current PE

      pe_pad_x = 0;
      pe_pad_y = 0;

      if(pe_n > N-1){
        pe_pad_x = (pe_n - N + 1) % pe_N;
        if(pe_pad_x == 0) {pe_pad_x = 1;}
      }
      if(pe_m > M-1){
        pe_pad_y = (pe_m - M + 1) % pe_M;
        if(pe_pad_y == 0) {pe_pad_y = 1;}
      }

      if((idx % 2) == 0 and (idy % 2 == 0)){
        @set_tile_code(idx, idy, "pe_program.csl", @concat_structs(.{
        .memcpy_params = memcpy.get_params(idx),
        .M = pe_M, .N = pe_N, .pad_x = pe_pad_x, .pad_y = pe_pad_y
        }, @concat_structs(common_params, @concat_structs(even_col_params, even_row_params))));
      }else if ((idx % 2) == 0 and (idy % 2 == 1)){
        @set_tile_code(idx, idy, "pe_program.csl", @concat_structs(.{
        .memcpy_params = memcpy.get_params(idx),
        .M = pe_M, .N = pe_N, .pad_x = pe_pad_x, .pad_y = pe_pad_y
        }, @concat_structs(common_params, @concat_structs(even_col_params, odd_row_params))));
      }else if((idx % 2) == 1 and (idy % 2 == 0)){
        @set_tile_code(idx, idy, "pe_program.csl", @concat_structs(.{
        .memcpy_params = memcpy.get_params(idx),
        .M = pe_M, .N = pe_N, .pad_x = pe_pad_x, .pad_y = pe_pad_y
        }, @concat_structs(common_params, @concat_structs(odd_col_params, even_row_params))));
      }else{
        @set_tile_code(idx, idy, "pe_program.csl", @concat_structs(.{
        .memcpy_params = memcpy.get_params(idx),
        .M = pe_M, .N = pe_N, .pad_x = pe_pad_x, .pad_y = pe_pad_y
        }, @concat_structs(common_params, @concat_structs(odd_col_params, odd_row_params))));
      }
    }
  }

  // Routing Configurations
  for (@range(i32, kernel_dim_x)) |idx| {
    for (@range(i32, kernel_dim_y)) |idy| {
      if (idx % 2 == 0){
        if(idx == 0){
          @set_color_config(idx, idy, send_east_color_1, .{.routes= .{ .rx = .{RAMP}, .tx = .{EAST}}});
          @set_color_config(idx, idy, send_west_color_2, .{.routes= .{ .rx = .{EAST}, .tx = .{RAMP}}});
        }else if(idx == kernel_dim_x - 1){
          @set_color_config(idx, idy, send_east_color_2, .{.routes= .{ .rx = .{WEST}, .tx = .{RAMP}}});
          @set_color_config(idx, idy, send_west_color_1, .{.routes= .{ .rx = .{RAMP}, .tx = .{WEST}}});
        }else{
          @set_color_config(idx, idy, send_east_color_1, .{.routes= .{ .rx = .{RAMP}, .tx = .{EAST}}});
          @set_color_config(idx, idy, send_east_color_2, .{.routes= .{ .rx = .{WEST}, .tx = .{RAMP}}});
          @set_color_config(idx, idy, send_west_color_1, .{.routes= .{ .rx = .{RAMP}, .tx = .{WEST}}});
          @set_color_config(idx, idy, send_west_color_2, .{.routes= .{ .rx = .{EAST}, .tx = .{RAMP}}});
        }
      }else{
        if(idx == kernel_dim_x - 1){
          @set_color_config(idx, idy, send_east_color_1, .{.routes= .{ .rx = .{WEST}, .tx = .{RAMP}}});
          @set_color_config(idx, idy, send_west_color_2, .{.routes= .{ .rx = .{RAMP}, .tx = .{WEST}}});
        }else{
          @set_color_config(idx, idy, send_east_color_1, .{.routes= .{ .rx = .{WEST}, .tx = .{RAMP}}});
          @set_color_config(idx, idy, send_east_color_2, .{.routes= .{ .rx = .{RAMP}, .tx = .{EAST}}});
          @set_color_config(idx, idy, send_west_color_1, .{.routes= .{ .rx = .{EAST}, .tx = .{RAMP}}});
          @set_color_config(idx, idy, send_west_color_2, .{.routes= .{ .rx = .{RAMP}, .tx = .{WEST}}});
        }
      }

      if(idy % 2 == 0){
        if(idy == 0){
          @set_color_config(idx, idy, send_north_color_2, .{.routes= .{ .rx = .{SOUTH}, .tx = .{RAMP}}});
          @set_color_config(idx, idy, send_south_color_1, .{.routes= .{ .rx = .{RAMP}, .tx = .{SOUTH}}});
        }else if(idy == kernel_dim_y-1){
          @set_color_config(idx, idy, send_north_color_1, .{.routes= .{ .rx = .{RAMP}, .tx = .{NORTH}}});
          @set_color_config(idx, idy, send_south_color_2, .{.routes= .{ .rx = .{NORTH}, .tx = .{RAMP}}});
        }else{
          @set_color_config(idx, idy, send_north_color_1, .{.routes= .{ .rx = .{RAMP}, .tx = .{NORTH}}});
          @set_color_config(idx, idy, send_north_color_2, .{.routes= .{ .rx = .{SOUTH}, .tx = .{RAMP}}});
          @set_color_config(idx, idy, send_south_color_1, .{.routes= .{ .rx = .{RAMP}, .tx = .{SOUTH}}});
          @set_color_config(idx, idy, send_south_color_2, .{.routes= .{ .rx = .{NORTH}, .tx = .{RAMP}}});
        }
      }else{
        if(idy == kernel_dim_y-1){
          @set_color_config(idx, idy, send_north_color_2, .{.routes= .{ .rx = .{RAMP}, .tx = .{NORTH}}});
          @set_color_config(idx, idy, send_south_color_1, .{.routes= .{ .rx = .{NORTH}, .tx = .{RAMP}}});
        }else{
          @set_color_config(idx, idy, send_north_color_2, .{.routes= .{ .rx = .{RAMP}, .tx = .{NORTH}}});
          @set_color_config(idx, idy, send_north_color_1, .{.routes= .{ .rx = .{SOUTH}, .tx = .{RAMP}}});
          @set_color_config(idx, idy, send_south_color_1, .{.routes= .{ .rx = .{NORTH}, .tx = .{RAMP}}});
          @set_color_config(idx, idy, send_south_color_2, .{.routes= .{ .rx = .{RAMP}, .tx = .{SOUTH}}});
        }
      }
    }
  }
  
  
  // export symbol names
  @export_name("A", [*]f32, true);
  @export_name("c", [*]f32, true);
  @export_name("compute", fn()void);

  @export_name("maxmin_time", [*]f32, true);
}
