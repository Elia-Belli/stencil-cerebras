
const sys_mod = @import_module("<memcpy/memcpy>", memcpy_params);
const ctrl = @import_module("<control>");
const layout_mod = @import_module("<layout>");
const timestamp_mod = @import_module("<time>");
const utils = @import_module("my_utils.csl", .{.width = width, .height = height});

param memcpy_params: comptime_struct;

// pe parameters  
param width: i32;
param height: i32;
param iterations: i64;

param M: i32; // local matrix rows
param N: i32; // local matrix cols
param pad_x: i32; // padding in x direction
param pad_y: i32; // padding in y direction

// Colors
param send_east_color: color;
param recv_west_color: color;
param send_west_color: color;
param recv_east_color: color;

param send_north_color: color;
param recv_south_color: color;
param send_south_color: color;
param recv_north_color: color;

// Queues IDs
// These input queues are bound to tasks for WSE-3,
// so they do not have associated microthreaded operations
const east_iq = @get_input_queue(2);
const west_iq = @get_input_queue(3);
const north_iq = @get_input_queue(4);
const south_iq = @get_input_queue(5);

// These queues are used in microthreaded operations on both WSE-2 and WSE-3
const east_oq = @get_output_queue(2);
const west_oq = @get_output_queue(3);
const north_oq = @get_output_queue(4);
const south_oq = @get_output_queue(5);

// Data Task IDs
// On WSE-2, data task IDs are created from colors; on WSE-3, from input queues
const recv_east_task_id : data_task_id =
  if      (@is_arch("wse2")) @get_data_task_id(recv_east_color)
  else if (@is_arch("wse3")) @get_data_task_id(east_iq);
const recv_west_task_id : data_task_id =
  if      (@is_arch("wse2")) @get_data_task_id(recv_west_color)
  else if (@is_arch("wse3")) @get_data_task_id(west_iq);
const recv_north_task_id : data_task_id =
  if      (@is_arch("wse2")) @get_data_task_id(recv_north_color)
  else if (@is_arch("wse3")) @get_data_task_id(north_iq);
const recv_south_task_id : data_task_id =
  if      (@is_arch("wse2")) @get_data_task_id(recv_south_color)
  else if (@is_arch("wse3")) @get_data_task_id(south_iq);

// Local Tasks IDs
const stencil_task_id : local_task_id = @get_local_task_id(10);
const north_id : local_task_id = @get_local_task_id(11);
const south_id : local_task_id = @get_local_task_id(12);
const east_id  : local_task_id = @get_local_task_id(13);
const west_id  : local_task_id = @get_local_task_id(14);
const sync_task_id : local_task_id = @get_local_task_id(15);

// Control Task IDs
const south_ctrl_id : control_task_id = @get_control_task_id(40);
const north_ctrl_id : control_task_id = @get_control_task_id(41);
const east_ctrl_id : control_task_id = @get_control_task_id(42);
const west_ctrl_id : control_task_id = @get_control_task_id(43);

//////////////////////////////////////////////////
//        48 kB of global memory                //    
//////////////////////////////////////////////////

var idx: i32;
var idy: i32;
var n_sides: i32;

const line: i32 = N + 2;
const col: i32 = M + 2;
const n: i32 = line*col;

var A     = @zeros([n]f32);
var A_aux = @zeros([n]f32);
var coeff = @zeros([5]f32);

// pointers 
var A_ptr:      [*]f32 = &A;
var A_aux_ptr:  [*]f32 = &A_aux;
var coeff_ptr:  [*]f32 = &coeff;
var temp:       [*]f32;

// timestamps
var tsc_end_buf   = @zeros([timestamp_mod.tsc_size_words]u16);
var tsc_start_buf = @zeros([timestamp_mod.tsc_size_words]u16);
var timer_buf     = @zeros([3]f32);
var ptr_timer_buf: [*]f32 = &timer_buf;

// DSDs
const east_out_dsd  = @get_dsd(fabout_dsd, .{ .extent = M, .fabric_color = send_east_color, .output_queue = east_oq});
const west_out_dsd  = @get_dsd(fabout_dsd, .{ .extent = M, .fabric_color = send_west_color, .output_queue = west_oq});
const north_out_dsd = @get_dsd(fabout_dsd, .{ .extent = N, .fabric_color = send_north_color, .output_queue = north_oq});
const south_out_dsd = @get_dsd(fabout_dsd, .{ .extent = N, .fabric_color = send_south_color, .output_queue = south_oq});

const north_ctrl_dsd = @get_dsd(fabout_dsd, .{.extent = 1, .fabric_color = send_north_color, .control = true, .output_queue = north_oq});
const south_ctrl_dsd = @get_dsd(fabout_dsd, .{.extent = 1, .fabric_color = send_south_color, .control = true, .output_queue = south_oq});
const east_ctrl_dsd  = @get_dsd(fabout_dsd, .{.extent = 1, .fabric_color = send_east_color, .control = true, .output_queue = east_oq});
const west_ctrl_dsd  = @get_dsd(fabout_dsd, .{.extent = 1, .fabric_color = send_west_color, .control = true, .output_queue = west_oq});

// Utils Functions

fn final_tsc() void {
  timestamp_mod.get_timestamp(&tsc_end_buf);
  timestamp_mod.disable_tsc();
  var lo_: u16 = 0;
  var hi_: u16 = 0;
  var word: i32 = 0;

  lo_ = tsc_start_buf[0];
  hi_ = tsc_start_buf[1];
  timer_buf[0] = @bitcast(f32, (@as(i32,hi_) << @as(u16,16)) | @as(i32, lo_) );

  lo_ = tsc_start_buf[2];
  hi_ = tsc_end_buf[0];
  timer_buf[1] = @bitcast(f32, (@as(i32,hi_) << @as(u16,16)) | @as(i32, lo_) );

  lo_ = tsc_end_buf[1];
  hi_ = tsc_end_buf[2];
  timer_buf[2] = @bitcast(f32, (@as(i32,hi_) << @as(u16,16)) | @as(i32, lo_) );
}

///////////////////////////////////////////////
//                PROGRAM                    //
///////////////////////////////////////////////

fn init() void {

  idx = @as(i32, layout_mod.get_x_coord());
  idy = @as(i32, layout_mod.get_y_coord());
  n_sides = utils.sides(idx, idy);

  timestamp_mod.enable_tsc();
  timestamp_mod.get_timestamp(&tsc_start_buf);

  send_halo();  // start compute
}

// SEND
fn send_halo() void {
  
  // halo DSDs
  var east_halo_dsd  = @get_dsd(mem1d_dsd, .{.base_address = &A_ptr[(2*N)+2],   .stride = N+2, .extent = M});
  var west_halo_dsd  = @get_dsd(mem1d_dsd, .{.base_address = &A_ptr[N+3],       .stride = N+2, .extent = M});
  var north_halo_dsd = @get_dsd(mem1d_dsd, .{.base_address = &A_ptr[N+3],       .stride = 1,   .extent = N});
  var south_halo_dsd = @get_dsd(mem1d_dsd, .{.base_address = &A_ptr[(N+2)*M +1],.stride = 1,   .extent = N});

  if(idx < width-1) { @fmovs(east_out_dsd, east_halo_dsd, .{ .async = true, .activate = east_id});}  // send east
  if(idx > 0)       { @fmovs(west_out_dsd, west_halo_dsd, .{ .async = true, .activate = west_id});}  // send west

  if(idy < height-1)  { @fmovs(south_out_dsd, south_halo_dsd,.{ .async = true, .activate = south_id});}  // send south
  if(idy > 0)         { @fmovs(north_out_dsd, north_halo_dsd,.{ .async = true, .activate = north_id});}  // send north  
}

// completed send callbacks
const north_ctrl_encoded = ctrl.encode_control_task_payload(south_ctrl_id);
const north_ctrl_encoded_dsd = @get_dsd(mem1d_dsd, .{.base_address = &north_ctrl_encoded, .extent = 1});
const south_ctrl_encoded = ctrl.encode_control_task_payload(north_ctrl_id);
const south_ctrl_encoded_dsd = @get_dsd(mem1d_dsd, .{.base_address = &south_ctrl_encoded, .extent = 1});
const east_ctrl_encoded = ctrl.encode_control_task_payload(west_ctrl_id);
const east_ctrl_encoded_dsd = @get_dsd(mem1d_dsd, .{.base_address = &east_ctrl_encoded, .extent = 1});
const west_ctrl_encoded = ctrl.encode_control_task_payload(east_ctrl_id);
const west_ctrl_encoded_dsd = @get_dsd(mem1d_dsd, .{.base_address = &west_ctrl_encoded, .extent = 1});

var send_completed: i32 = 0;
task north_end() void { 
  send_completed += 1; 
  @mov32(north_ctrl_dsd, north_ctrl_encoded_dsd, .{.async = true, .activate = sync_task_id});
}

task south_end() void {
  send_completed += 1;
  @mov32(south_ctrl_dsd, south_ctrl_encoded_dsd, .{.async = true, .activate = sync_task_id});
}

task east_end() void { 
  send_completed += 1; 
  @mov32(east_ctrl_dsd, east_ctrl_encoded_dsd, .{.async = true, .activate = sync_task_id});
}

task west_end() void { 
  send_completed += 1;
  @mov32(west_ctrl_dsd, west_ctrl_encoded_dsd, .{.async = true, .activate = sync_task_id});
}

// RECV
var west_count:  i32 = 0;
var east_count:  i32 = 0;
var north_count: i32 = 0;
var south_count: i32 = 0;

task recv_west(data:f32) void{
  west_count += 1;
  A_ptr[(west_count)*(N+2)] = data;
}

task recv_east(data:f32) void{
  east_count += 1;
  A_ptr[(east_count+1)*(N+2)-1] = data;
}

task recv_north(data:f32) void{
  north_count += 1;
  A_ptr[north_count] = data;
}

task recv_south(data:f32) void{
  south_count += 1;
  A_ptr[(N+2)*(M+1) + south_count] = data;
}

// completed recv callback : activate by control wavelets
task south_ctrl() void {
  @block(recv_south_task_id);
  south_count = 0;
  recv_callback();
}

task north_ctrl() void {
  @block(recv_north_task_id);
  north_count = 0;
  recv_callback();
}

task west_ctrl() void {
  @block(recv_west_task_id);
  west_count = 0;
  recv_callback();
}

task east_ctrl() void {
  @block(recv_east_task_id);
  east_count = 0;
  recv_callback();
}

// completed recv callback --> complete all & wait for sends to finish
var recv_completed: i32 = 0;
fn recv_callback() void {
  recv_completed += 1;
  @activate(sync_task_id);
}

// wait for all sends and recvs to finish --> COMPUTE
task sync() void {
  if(send_completed == n_sides and recv_completed == n_sides){

    recv_completed = 0;
    send_completed = 0;

    @activate(stencil_task_id);
  }
}

// COMPUTE
var iter: i64 = 0;
task stencil() void {

  // center
  const aux_dsd = @get_dsd(mem4d_dsd, .{.base_address = &A_aux_ptr[N+3], .stride = .{1,3}, .extent = .{M,N}});
  var a_dsd = @get_dsd(mem4d_dsd, .{.base_address = &A_ptr[N+3], .stride = .{1,3}, .extent = .{M,N}});
  @fmuls(aux_dsd, a_dsd, coeff[2]);

  // north
  a_dsd = @increment_dsd_offset(a_dsd, -(N+2), f32);  // base = 1
  @fmacs(aux_dsd, aux_dsd, a_dsd, coeff[0]);

  // south
  a_dsd = @increment_dsd_offset(a_dsd, (N+2)*2, f32);  // base = (N+2)*2 + 1
  @fmacs(aux_dsd, aux_dsd, a_dsd, coeff[4]);

  // west
  a_dsd = @increment_dsd_offset(a_dsd, -(N+3), f32);  // base = (N+2)
  @fmacs(aux_dsd, aux_dsd, a_dsd, coeff[1]);

  // east
  a_dsd = @increment_dsd_offset(a_dsd, 2, f32);  // base = (N+4)
  @fmacs(aux_dsd, aux_dsd, a_dsd, coeff[3]);

  // reset input point to original value
  if(idx == idy){
    for(@range(i32,0, M, 1)) |i|{
      A_aux_ptr[(1+i)*line + i + 1] = A_ptr[(1+i)*line + i + 1];
    }
  }

  // reset pad values to 0
  if(pad_x > 0){
    var pad_x_dsd = @get_dsd(mem4d_dsd, .{.base_address = &A_aux_ptr[1+N-pad_x], .stride = .{1, line-pad_x+1}, .extent = .{col, pad_x}});
    @fmovs(pad_x_dsd, 0.0);
  }
  if(pad_y > 0){
    var pad_y_dsd = @get_dsd(mem1d_dsd, .{.base_address = &A_aux_ptr[(1+M-pad_y)*line], .stride = 1, .extent = line*pad_y});
    @fmovs(pad_y_dsd, 0.0);
  }

  temp  = A_ptr;
  A_ptr = A_aux_ptr;
  A_aux_ptr = temp;

  @unblock(recv_north_task_id);
  @unblock(recv_south_task_id);
  @unblock(recv_west_task_id);
  @unblock(recv_east_task_id);

  iter += 1;
  if(iter == iterations){
    final_tsc();  // completion timestamp
    sys_mod.unblock_cmd_stream();
  }else{
    send_halo();
  }
}

comptime {
  @bind_local_task(stencil, stencil_task_id);

  @bind_local_task(east_end, east_id);
  @bind_local_task(west_end, west_id);
  @bind_local_task(north_end, north_id);
  @bind_local_task(south_end, south_id);
  @bind_local_task(sync, sync_task_id);

  @bind_data_task(recv_west, recv_west_task_id);
  @bind_data_task(recv_east, recv_east_task_id);
  @bind_data_task(recv_north, recv_north_task_id);
  @bind_data_task(recv_south, recv_south_task_id);

  // Control tasks : no need for explicit unblock since they use data task colors
  @bind_control_task(south_ctrl, south_ctrl_id);
  @bind_control_task(north_ctrl, north_ctrl_id);
  @bind_control_task(east_ctrl, east_ctrl_id);
  @bind_control_task(west_ctrl, west_ctrl_id);


  // On WSE-3, we must explicitly initialize input and output queues
  if (@is_arch("wse3")) {
    @initialize_queue(east_oq,  .{ .color = send_east_color });
    @initialize_queue(west_oq,  .{ .color = send_west_color });
    @initialize_queue(north_oq, .{ .color = send_north_color });
    @initialize_queue(south_oq, .{ .color = send_south_color });
    
    @initialize_queue(east_iq,  .{ .color = recv_east_color });
    @initialize_queue(west_iq,  .{ .color = recv_west_color });
    @initialize_queue(north_iq, .{ .color = recv_north_color });
    @initialize_queue(south_iq, .{ .color = recv_south_color });
  }

  @export_symbol(A_ptr, "A");
  @export_symbol(coeff_ptr, "c");
  @export_symbol(init, "compute");
  @export_symbol(ptr_timer_buf, "maxmin_time");
}
